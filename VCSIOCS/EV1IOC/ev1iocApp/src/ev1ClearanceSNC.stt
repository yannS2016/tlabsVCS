program ev1ClearanceSNC

%{
#include "vcs_var_mes.h" // define this here so C functions are seen
#define STOK      0
#define FAIL      -1
#define WFVC      55
#define	B19OFF    102
#define	B18ON     99
#define DA6S1OFF  100 
#define DA6S2OFF  101
#define RA6ON     144
#define RA7ON     145 
#define EV1PNK    131 
}%
/* Declare seq variables*/
/* Declare seq variables*/
short EV1_INIT; 
short EV1_VC;   
short EV1_W18_STS;
short EV1_W19_STS;
short EV1_FAIL_COND;
short EV1_PNK;
short EV1_RST_OUTS;
string EV1_CLR_STATS; 
string EV1_INTLCK_STATS;
// CONTROL FLAGS
short ev1_failed_rst;
short EV1_OLD_FAIL_COND;
short ev1_not_cleared = FALSE;

/* Associate SNL variables with epics output pv's*/
assign	EV1_RST_OUTS        to	"{SYS}:RST-EV1.PROC"; 
assign	EV1_CLR_STATS       to	"{SYS}:EV1-CLR-STATS"; 
assign	EV1_INTLCK_STATS	  to	"{SYS}:EV1-INTLCK-STATS"; 
/* Associate SNL variables with epics input pv's*/
assign 	EV1_VC 		          to 	"{SYS}:EV1-VC"; 
assign 	EV1_W18_STS 		    to 	"{SYS}:EV1-W18-STS";   
assign 	EV1_W19_STS 		    to 	"{SYS}:EV1-W19-STS";
assign	EV1_FAIL_COND		    to  "{SYS}:EV1-FAIL-COND";
assign	EV1_PNK		          to	"{SYS}:EV1-PNK";

monitor	EV1_VC;
monitor EV1_W18_STS;  
monitor EV1_W19_STS;
monitor	EV1_FAIL_COND;
monitor	EV1_PNK;

ss EV1_clearance_status {
    
    state init {
		  when (delay(1)) {
			  initVault();
			  printf("\n%sEXPERIMENTAL VAULT 1 INITIALISATION COMPLETED%s\n",OK,DEF);
		  }   state watchman_18_pressed	
    }
//////////////////////////////////////////////////////////
	state watchman_18_pressed {
 
    when (EV1_FAIL_COND != STOK) {
      EV1_OLD_FAIL_COND = EV1_FAIL_COND;
    } state  clearance_faillures
     
    
		when (EV1_W18_STS == WFVC) {

		} state clearance_pending  
			
		when (EV1_W18_STS == B18ON) {
			EV1_clear_underway();
			EV1_update_report(B18ON);
		} state watchman_18_released
	}
 ////////////////////////////////////////////////////////////////
	state clearance_pending {
		option -e;
		option -x;
		entry {
      resetVault();
      EV1_update_report(WFVC);
      ev1_not_cleared = TRUE; // we use this to tell user to start or restart vault clearance
		}
		when (EV1_W18_STS != WFVC) {

		} state watchman_18_pressed  		
	}
 //////////////////////////////////////////////////////////
	state watchman_18_released {
		
		when (EV1_FAIL_COND != STOK) {

		} state  clearance_faillures
			
		when (EV1_W18_STS != B18ON) {
   
		} state a6_switch_I_closed
	}   
//////////////////////////////////////////////////////////
	state a6_switch_I_closed {
        
		option -e;

		entry {
      EV1_update_report(DA6S1OFF);
		} 

		when (EV1_FAIL_COND != STOK) {

		} state   clearance_faillures
		
		when (EV1_W19_STS != DA6S1OFF) {
            
		} state a6_switch_II_closed
        
	}
 //////////////////////////////////////////////////////////
	state a6_switch_II_closed {
 
		option -e;

		entry {
      EV1_update_report(DA6S2OFF);
		} 

		when (EV1_FAIL_COND != STOK) {

		} state   clearance_faillures
   
   // previous switch might have opened up
		when (EV1_W19_STS == DA6S1OFF) {

		} state a6_switch_I_closed
		
		when (EV1_W19_STS != DA6S2OFF) {

		} state watchman_19_pressed
	}
///////////////////////////////////////////////////////////
	state watchman_19_pressed {
 
		option -e;

		entry {
      EV1_update_report(B19OFF);
		} 
		
		when (EV1_FAIL_COND != STOK) {

		} state  clearance_faillures
			
			
		when (EV1_W19_STS == STOK) {
		} state vault_cleared
   
   /// must be last
		when (EV1_W19_STS != B19OFF) {
            
		} state a6_switch_I_closed 
	} 
///////////////////////////////////////////////////////////////
  state vault_cleared {
		option -e;
		entry {
      EV1_status();
			printf("%sEXPERIMENTAL VAULT 1 IN CLEARED STATE%s\n",CLR,DEF);
		} 
		when (EV1_VC != STOK) {

		} state paniq_faillure
	
	} 
 ///////////////////////////////////////////////////   
  state paniq_faillure {

    when (EV1_VC != EV1PNK) {

		} state clearance_faillures	
 
    when () { // paniq button released?
      printf("EV1 VAULT CLEARED FAILURE CONDITION: ");
      EV1_update_failed(EV1PNK); 
		} state recover_from_paniq	     
	} 
////////////////////////////////////////////////////////////    
  state recover_from_paniq {
		option -e;
		option -x;
		when (!EV1_PNK) { // paniq button released?
        // we only go to this state so we enter watchman_18_pressed from one point(recover_from_faillure)
		} state recover_from_faillure  

		exit {
      ev1_failed_rst = FALSE;
      ev1_not_cleared = FALSE;
      //printf("%sEV1 RECOVERING FROM PANIQ FAILLURE : ENTERING W18 STATE%s\n",OK,DEF);
		}
	} 
 ///////////////////////////////////////////////////   
  state clearance_faillures {
		option -e;
		option -x;
		entry {
      printf("EV1 CLEARANCE FAILURE CONDITION: ");
      ev1_failed_rst = FALSE;
      ev1_not_cleared = FALSE;
      EV1_OLD_FAIL_COND = EV1_FAIL_COND;
      EV1_update_failed(EV1_FAIL_COND == FAIL ? STOK : EV1_FAIL_COND); // we cannot index -1(FAIL)
		}
   
		when () { // seq excute faster than recor logi
		} state recover_from_faillure	   
	} 
//////////////////////////////////////////////////////////
	state recover_from_faillure {
		
		when (EV1_FAIL_COND ==  STOK) {
      printf("%sEV1 RECOVERING FROM CLEARANCE  FAILLURE : ENTERING W18 STATE%s\n",OK,DEF);
		} state  watchman_18_pressed
			
		when (EV1_FAIL_COND != EV1_OLD_FAIL_COND) {
           
		} state  clearance_faillures
	}   
}
///////////////////////////////////////////////////////////////////////////////
void initVault(){
	EV1_OLD_FAIL_COND = EV1_FAIL_COND;
    EV1_RST_OUTS  = TRUE;
	not_cleared(EV1_CLR_STATS);
	report_error(EV1_INTLCK_STATS, 0);
    pvPut(EV1_RST_OUTS, SYNC);
	pvPut(EV1_CLR_STATS, SYNC);
	pvPut(EV1_INTLCK_STATS, SYNC);
}
////////////////////////////////////////////////////////////////////////
void resetVault(){
	not_cleared(EV1_CLR_STATS);
	pvPut(EV1_CLR_STATS, SYNC);
}
////////////////////////////////////////////////////////////////////////
void EV1_update_failed(short report)
{
	clear_failed(EV1_CLR_STATS);
	pvPut(EV1_CLR_STATS, SYNC);
	report_error(EV1_INTLCK_STATS, report);
	pvPut(EV1_INTLCK_STATS, SYNC);
	printf("%s%s%s\n",ERROR,EV1_INTLCK_STATS,DEF);
}
/////////////////////////////////////////////////////////////////////////
void EV1_update_report(short intlck) {
	report_error(EV1_INTLCK_STATS, intlck);
	pvPut(EV1_INTLCK_STATS, SYNC);
}
////////////////////////////////////////////////////////////////////////
void EV1_status()
{
	cleared(EV1_CLR_STATS);
	pvPut(EV1_CLR_STATS, SYNC); 		
	report_error(EV1_INTLCK_STATS, 0);
	pvPut(EV1_INTLCK_STATS, SYNC);
}
///////////////////////////////////////////////////////////
void EV1_clear_underway()
{  
	clear_underway(EV1_CLR_STATS);
	pvPut(EV1_CLR_STATS, SYNC); 		
}

//////////////////////////////////////////////////////////////////////////
%{

inline void cleared(string vault_status) {   
    strcpy(vault_status, mes_array[1]); // Area cleared  
}
////////////////////////////////////////////////
inline void not_cleared(string vault_status) {  
   strcpy(vault_status, mes_array[2]); // Area not cleared    
}
///////////////////////////////////////////////
inline void clear_underway(string vault_status) { 
   strcpy(vault_status, mes_array[3]);  // Area clearance underway    
}
//////////////////////////////////////////////
inline void clear_failed(string vault_status) { 
   strcpy(vault_status, mes_array[5]); // Area clearance fail   
}
////////////////////////////////////////////////
inline void report_error(string vault_interlock, short intlock)
{
   strcpy(vault_interlock, mes_array[intlock]);
}
	
}%