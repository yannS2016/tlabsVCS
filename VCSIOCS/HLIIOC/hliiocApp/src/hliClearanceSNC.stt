program hliClearanceSNC

%{
#include "vcs_var_mes.h" // define this here so C functions are seen
#define STOK        0
#define WFVC        55
#define	B31OFF      92
#define	B30ON       89
#define DA14S1OFF   90
#define DA14S2OFF   91
#define HLIPNK      134
}%

/* Declare seq variables*/
/* Declare seq variables*/
short HLI_INIT; 
short HLI_VC;   
short HLI_W30_STS;
short HLI_W31_STS;
short HLI_FAIL_COND;
short HLI_PNK;
short HLI_RST_OUTS;
string HLI_CLR_STATS; 
string HLI_INTLCK_STATS;
// CONTROL FLAGS
short HLI_failed_rst;
short HLI_OLD_FAIL_COND;
short HLI_not_cleared = FALSE;

/* Associate SNL variables with epics output pv's*/
assign	HLI_RST_OUTS        to	"{SYS}:RST-HLI.PROC"; 
assign	HLI_CLR_STATS       to	"{SYS}:HLI-CLR-STATS"; 
assign	HLI_INTLCK_STATS	  to  "{SYS}:HLI-INTLCK-STATS"; 

/* Associate SNL variables with epics input pv's*/
assign 	HLI_VC 		          to 	"{SYS}:HLI-VC"; 
assign 	HLI_W30_STS 		    to 	"{SYS}:HLI-W30-STS";   
assign 	HLI_W31_STS 		    to 	"{SYS}:HLI-W31-STS";
assign	HLI_FAIL_COND		    to  "{SYS}:HLI-FAIL-COND";
assign	HLI_PNK		          to  "{SYS}:HLI-PNK";

monitor	HLI_VC;
monitor HLI_W30_STS;  
monitor HLI_W31_STS;
monitor	HLI_FAIL_COND;
monitor	HLI_PNK;

ss HLI_clearance_status {
    
    state init {
		  when (delay(1)) {
			  initVault();
			  printf("\n%sHLI VAULT INITIALISATION COMPLETED%s\n",OK,DEF);
		  }   state watchman_30_pressed	
    }
//////////////////////////////////////////////////////////
	state watchman_30_pressed {
 
    when (HLI_FAIL_COND != STOK) {
      HLI_OLD_FAIL_COND = HLI_FAIL_COND;
    } state  clearance_faillures
     
    
		when (HLI_W30_STS == WFVC) {

		} state clearance_pending  
			
		when (HLI_W30_STS == B30ON) {
			HLI_clear_underway();
			HLI_update_report(B30ON);
		} state watchman_30_released
	}
 ////////////////////////////////////////////////////////////////
	state clearance_pending {
		option -e;
		option -x;
		entry {
      resetVault();
      HLI_update_report(WFVC);
      HLI_not_cleared = TRUE; // we use this to tell user to start or restart vault clearance
		}
		when (HLI_W30_STS != WFVC) {

		} state watchman_30_pressed  		
	}
 //////////////////////////////////////////////////////////
	state watchman_30_released {
		
		when (HLI_FAIL_COND != STOK) {

		} state  clearance_faillures
			
		when (HLI_W30_STS != B30ON) {
   
		} state A14_switch_I_closed
	}   
//////////////////////////////////////////////////////////
	state A14_switch_I_closed {
        
		option -e;

		entry {
      HLI_update_report(DA14S1OFF);
		} 

		when (HLI_FAIL_COND != STOK) {

		} state   clearance_faillures
		
		when (HLI_W31_STS != DA14S1OFF) {
            
		} state A14_switch_II_closed
        
	}
 //////////////////////////////////////////////////////////
	state A14_switch_II_closed {
 
		option -e;

		entry {
      HLI_update_report(DA14S2OFF);
		} 

		when (HLI_FAIL_COND != STOK) {

		} state   clearance_faillures
   
   // previous switch might have opened up
		when (HLI_W31_STS == DA14S1OFF) {

		} state A14_switch_I_closed
		
		when (HLI_W31_STS != DA14S2OFF) {

		} state watchman_31_pressed
	}
///////////////////////////////////////////////////////////
	state watchman_31_pressed {
 
		option -e;

		entry {
      HLI_update_report(B31OFF);
		} 
		
		when (HLI_FAIL_COND != STOK) {

		} state  clearance_faillures
			
			
		when (HLI_W31_STS == STOK) {
		} state vault_cleared
   
   /// must be last
		when (HLI_W31_STS != B31OFF) {
            
		} state A14_switch_I_closed 
	} 
///////////////////////////////////////////////////////////////
  state vault_cleared {
		option -e;
		entry {
      HLI_status();
			printf("%sHLI VAULT IN CLEARED STATE%s\n",CLR,DEF);
		} 
		when (HLI_VC != STOK) {

		} state paniq_faillure
	
	} 
 ///////////////////////////////////////////////////   
  state paniq_faillure {

    when (HLI_VC != HLIPNK) {

		} state clearance_faillures	
 
    when () { // paniq button released?
      printf("HLI VAULT CLEARED FAILURE CONDITION: ");
      HLI_update_failed(HLIPNK); 
		} state recover_from_paniq	     
	} 
////////////////////////////////////////////////////////////    
  state recover_from_paniq {
		option -e;
		option -x;
		when (!HLI_PNK) { // paniq button released?
        // we only go to this state so we enter watchman_30_pressed from one point(recover_from_faillure)
		} state recover_from_faillure  

		exit {
      HLI_failed_rst = FALSE;
      HLI_not_cleared = FALSE;
      //printf("%sHLI RECOVERING FROM PANIQ FAILLURE : ENTERING W30 STATE%s\n",OK,DEF);
		}
	} 
 ///////////////////////////////////////////////////   
  state clearance_faillures {
		option -e;
		option -x;
		entry {
            printf("HLI CLEARANCE FAILURE CONDITION: ");
            HLI_failed_rst = FALSE;
            HLI_not_cleared = FALSE;
            HLI_OLD_FAIL_COND = HLI_FAIL_COND;
            HLI_update_failed(HLI_FAIL_COND); // we cannot index -1(FAIL)
		}
   
		when () { // seq excute faster than recor logi
		} state recover_from_faillure	   
	} 
//////////////////////////////////////////////////////////
	state recover_from_faillure {
		
		when (HLI_FAIL_COND ==  STOK) {
      printf("%sHLI RECOVERING FROM CLEARANCE  FAILLURE : ENTERING W30 STATE%s\n",OK,DEF);
		} state  watchman_30_pressed
			
		when (HLI_FAIL_COND != HLI_OLD_FAIL_COND) {
           
		} state  clearance_faillures
	}   
}
///////////////////////////////////////////////////////////////////////////////
void initVault(){
  HLI_OLD_FAIL_COND = HLI_FAIL_COND;
  HLI_RST_OUTS  = TRUE;
	not_cleared(HLI_CLR_STATS);
	report_error(HLI_INTLCK_STATS, 0);
  pvPut(HLI_RST_OUTS, SYNC);
	pvPut(HLI_CLR_STATS, SYNC);
	pvPut(HLI_INTLCK_STATS, SYNC);
}
////////////////////////////////////////////////////////////////////////
void resetVault(){
	not_cleared(HLI_CLR_STATS);
	pvPut(HLI_CLR_STATS, SYNC);
}
////////////////////////////////////////////////////////////////////////
void HLI_update_failed(short report)
{
	clear_failed(HLI_CLR_STATS);
	pvPut(HLI_CLR_STATS, SYNC);
	report_error(HLI_INTLCK_STATS, report);
	pvPut(HLI_INTLCK_STATS, SYNC);
	printf("%s%s%s\n",ERROR,HLI_INTLCK_STATS,DEF);
}
/////////////////////////////////////////////////////////////////////////
void HLI_update_report(short intlck) {
	report_error(HLI_INTLCK_STATS, intlck);
	pvPut(HLI_INTLCK_STATS, SYNC);
}
////////////////////////////////////////////////////////////////////////
void HLI_status()
{
	cleared(HLI_CLR_STATS);
	pvPut(HLI_CLR_STATS, SYNC); 		
	report_error(HLI_INTLCK_STATS, 0);
	pvPut(HLI_INTLCK_STATS, SYNC);
}
///////////////////////////////////////////////////////////
void HLI_clear_underway()
{  
	clear_underway(HLI_CLR_STATS);
	pvPut(HLI_CLR_STATS, SYNC); 		
}

//////////////////////////////////////////////////////////////////////////
%{

inline void cleared(string vault_status) {   
    strcpy(vault_status, mes_array[1]); // Area cleared  
}
////////////////////////////////////////////////
inline void not_cleared(string vault_status) {  
   strcpy(vault_status, mes_array[2]); // Area not cleared    
}
///////////////////////////////////////////////
inline void clear_underway(string vault_status) { 
   strcpy(vault_status, mes_array[3]);  // Area clearance underway    
}
//////////////////////////////////////////////
inline void clear_failed(string vault_status) { 
   strcpy(vault_status, mes_array[5]); // Area clearance fail   
}
////////////////////////////////////////////////
inline void report_error(string vault_interlock, short intlock)
{
   strcpy(vault_interlock, mes_array[intlock]);
}
	
}%